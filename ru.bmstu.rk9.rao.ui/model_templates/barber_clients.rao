enum Тип_клиента {Тип1, Тип2}
enum Состояние_клиента {Пришел, Начал_стрижку}
enum Состояние_парикмахера {Свободен, Занят}

type Парикмахерские {
	int количество_в_очереди;
}

type Клиенты {
	barber_clients.Тип_клиента тип;
	barber_clients.Состояние_клиента состояние;
}

type Парикмахеры {
	barber_clients.Состояние_парикмахера состояние_парикмахера = Состояние_парикмахера.Свободен;
	int количество_обслуженных;
	int длительность_min;
	int длительность_max;
	barber_clients.Тип_клиента тип_клиента;
}

resource парикмахерская = Парикмахерские.create(0);
resource парикмахер_1 = Парикмахеры.create(Состояние_парикмахера.Свободен, 0, 20, 40, Тип_клиента.Тип1);
resource парикмахер_2 = Парикмахеры.create(Состояние_парикмахера.Свободен, 0, 25, 70, Тип_клиента.Тип2);
resource парикмахер_3 = Парикмахеры.create(Состояние_парикмахера.Свободен, 0, 30, 60, Тип_клиента.Тип2);


event Событие_прихода_клиента() {
	Клиенты.create(Случайный_тип_клиента.next(), Состояние_клиента.Пришел);
	Событие_прихода_клиента.plan(currentTime + Интервал_прихода.next());
	парикмахерская.количество_в_очереди = парикмахерская.количество_в_очереди + 1;
}

operation Образец_обслуживания_клиента() {
	relevant _Парикмахерская = парикмахерская.onlyif[количество_в_очереди > 0];
	relevant _Клиент = Клиенты.all.filter[состояние.equals(Состояние_клиента.Пришел)].any;
	relevant _Парикмахер = Парикмахеры.all.filter[состояние_парикмахера.equals(Состояние_парикмахера.Свободен)
			&& тип_клиента.equals(_Клиент.тип)].minBySafe[количество_обслуженных];

	set duration() {
		return Длительность_обслуживания.next(_Парикмахер.длительность_min, _Парикмахер.длительность_max);
	}

	set begin() {
		_Парикмахерская.количество_в_очереди = _Парикмахерская.количество_в_очереди - 1;
		_Клиент.состояние = Состояние_клиента.Начал_стрижку;
		_Парикмахер.состояние_парикмахера = Состояние_парикмахера.Занят;
	}

	set end() {
		_Парикмахер.состояние_парикмахера = Состояние_парикмахера.Свободен;
		_Парикмахер.количество_обслуженных = _Парикмахер.количество_обслуженных + 1;
		_Клиент.erase();
	}
}

logic model {
	activity Обслуживание_клиента = new Activity(Образец_обслуживания_клиента.create());
}

sequence Интервал_прихода = new Exponential(123456789, 1/30.0);
sequence Длительность_обслуживания = new Uniform(123456789);
sequence Случайный_тип_клиента = new DiscreteHistogram<barber_clients.Тип_клиента>(123456789,
	#[
		Тип_клиента.Тип1 -> 1.0,
		Тип_клиента.Тип2 -> 5.0
	]
);

set init() {
	Событие_прихода_клиента.plan(currentTime + Интервал_прихода.next());
}

set terminateCondition() {
	return currentTime >= 7 * 12 * 60;
}

//result Занятость_парикмахера_1 = watchState(Парикмахер_1.состояние_парикмахера == Состояние_парикмахера.Занят);
//result Занятость_парикмахера_2 = watchState(Парикмахер_2.состояние_парикмахера == Состояние_парикмахера.Занят);
//result Занятость_парикмахера_3 = watchState(Парикмахер_3.состояние_парикмахера == Состояние_парикмахера.Занят);
//result Обслужено_парикмахером_1 = getValue(Парикмахер_1.количество_обслуженных);
//result Обслужено_парикмахером_2 = getValue(Парикмахер_2.количество_обслуженных);
//result Обслужено_парикмахером_3 = getValue(Парикмахер_3.количество_обслуженных);
